#!/usr/bin/perl
#
# Format output generated by flex 2.5.31
#
# Usage:
#  flex -o$output $input
#  perl fix-flex $output > $tmp
#  mv $tmp $output
#
# (C) Copyright 2004 Dave Beckett http://purl.org/net/dajobe/
# (C) Copyright 2004 University of Bristol
#

my $line_offset=1; # #line directives always refer to the NEXT line

print <<'EOT';
#ifdef HAVE_CONFIG_H
#include <raptor_config.h>
#endif

#ifdef WIN32
#include <win32_raptor_config.h>
#endif

EOT
$line_offset+=8; # added 8 lines to output

while(<>) {
  # the default parser tries to free(NULL)
  s/(^\s+)(free\(.*ptr.*\))/${1}if(ptr) $2/;

  # flex has %option nounistd however it does not work in 2.5.31
  # It is safe to add yet another wrapper. 
  if(m%^(\#include \<unistd.h\>)$%) {
    $_=<<"EOT"; ;
#ifndef YY_NO_UNISTD_H
$1
#endif
EOT
    $line_offset+=2; # added 2 lines to output
  }

  # Fix .[ch] line references because we have added lines to it
  my $line = $. +$line_offset;
  s/^#line \d+ (\".*\.[ch]\")/#line $line $1/;

  print;
}
